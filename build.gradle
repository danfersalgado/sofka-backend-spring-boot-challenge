plugins {
    id 'org.springframework.boot' version '3.2.1' apply false
    id 'info.solidsoft.pitest' version '1.15.0' apply false
}

allprojects {
    group = 'com.sofka'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'jacoco'
    apply plugin: 'info.solidsoft.pitest'

    java {
        sourceCompatibility = '17'
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:3.2.1')
        annotationProcessor platform('org.springframework.boot:spring-boot-dependencies:3.2.1')
        compileOnly platform('org.springframework.boot:spring-boot-dependencies:3.2.1')
        testImplementation platform('org.springframework.boot:spring-boot-dependencies:3.2.1')

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-amqp'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
        implementation 'com.h2database:h2'
        runtimeOnly 'org.postgresql:postgresql'
        implementation 'org.mockito:mockito-core'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        
        pitest 'org.pitest:pitest-junit5-plugin:1.2.1'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }

    pitest {
        targetClasses = ["com.sofka.*"]
        excludedClasses = ["com.sofka.accountservice.config.*", "com.sofka.clientservice.config.*"]
        pitestVersion = '1.15.0'
        threads = 4
        outputFormats = ['XML', 'HTML']
        timestampedReports = false
        junit5PluginVersion = '1.2.1'
    }

    tasks.withType(info.solidsoft.gradle.pitest.PitestTask).configureEach {
        doNotTrackState("No track state for OneDrive compatibility")
    }

    tasks.named('check') {
        dependsOn 'pitest'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}
